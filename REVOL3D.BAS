5 CLS
6 SCREEN 2
7 WINDOW SCREEN (0,0)-(700,450)
10 KEY OFF
20 PI=4*ATN(1)
60 DIM CIBLE(25)
70 DIM R(40),Y(40),H(36),V(36)
80 LOCATE 1,61:PRINT"ENTREE DES SOMMETS"
90 LOCATE 2,64:PRINT"DU PROFIL DE"
95 LOCATE 3,65:PRINT"GENERATION"
100 LOCATE 6,60:PRINT"d‚placements de la"
105 LOCATE 7,60:PRINT"cible avec le pav‚"
106 LOCATE 8,60:PRINT"num‚rique :"
107 LOCATE 10,62:PRINT"directions:  4   6"
108 LOCATE 9,75:PRINT"7 8 9"
109 LOCATE 11,75:PRINT"1 2 3"
110 LOCATE 12,62:PRINT"pas:  + ou -"
111 LOCATE 15,60:PRINT"valider les points"
112 LOCATE 16,60:PRINT"avec ENTREE"
113 LOCATE 19,60:PRINT"valider le profil"
114 LOCATE 20,60:PRINT"avec ECHAP"
115 LOCATE 23,60:PRINT"40 points au maximum"
210 VIEW SCREEN(1,1)-(470,199),,1 :CLS
260 LINE (260,  0)-(260,450),1,,&HF99F
310 LINE (100,98)-(100 ,102),1
360 LINE (98 ,100)-(102,100),1
410 PSET (100,100),0
460 GET (98,98)-(102,102), CIBLE
510 LINE (100,98)-(100 ,102),0
560  LINE (98 ,100)-(102,100),0
610 X=258:Y=200:P=10:S=0
650 S=S+1
660 PUT (X  ,Y  ),CIBLE
710 A$=INKEY$:IF A$="" THEN 710
760 AX=X :AY=Y
810 IF A$="1"  AND X>P AND Y<337-P THEN X=X-P:Y=Y+P
860 IF A$="2"  AND Y<337-P THEN Y=Y+P
910 IF A$="3"  AND X<259-P AND Y<337-P  THEN X=X+P:Y=Y+P
960 IF A$="4"  AND X>P THEN X=X-P
1010 IF A$="6"  AND X<259-P THEN X=X+P
1060 IF A$="7"  AND X>P AND Y>P THEN X=X-P:Y=Y-P
1110 IF A$="8"  AND Y>P THEN Y=Y-P
1160 IF A$="9"  AND X<259-P AND Y>P THEN X=X+P:Y=Y-P
1210 IF A$="+"  THEN P=10
1260 IF A$="-"  THEN P=1
1300 PUT (AX ,AY ),CIBLE,XOR
1310 IF A$=CHR$(13) THEN LINE (X,Y)-(X+4,Y+4):LINE (X,Y+4)-(X+4,Y):R(S)=258-X:Y(S)=Y+2:GOTO 650
1320 IF A$=CHR$(27) THEN 1500
1410 GOTO 660
1500 VIEW:CLS
1504 LINE (0,0)-(521,341),,B
1505 LINE (260,  0)-(260,340),1,,&HF99F
1506 FOR K=1 TO S-2
1507 LINE (258-R(K),Y(K))-(258-R(K+1),Y(K+1))
1508 NEXT
1510 LOCATE 6,62:PRINT"Angle de rotation "
1515 LOCATE 7,62:PRINT"en degr‚s       "
1520 LOCATE 7,72:INPUT AN$
1530 IF AN$="" THEN RUN ELSE CLS
1535 VIEW SCREEN(1,1)-(480,199) :CLS
1540 AN=VAL(AN$)*PI/180
1550 C=.65*SIN(AN):AXE=COS(AN)
1560 LOCATE 25,63:PRINT"Tranche nø"
1570 FOR W=1 TO S-1
1580 R=R(W)
1590 LOCATE 25,74:PRINT W
1600 YR=166-(166-Y(W))*AXE
1610 GOSUB 2000
1620 NEXT
1630 LOCATE 25,65:PRINT"TERMINE"
1640 GOTO 1510
2000 VIEW SCREEN(1,1)-(480,199)
2005 FOR I=10 TO 360 STEP 10
2010 LT=I/10
2020 XA=260+R*SIN(I*PI/180)
2030 XP=260+R*SIN((I+10)*PI/180)
2040 YA=YR -R*COS(I*PI/180)*C
2050 YP=YR -R*COS((I+10)*PI/180) *C
2060 PSET(XA,YA)
2070 LINE(XA,YA)-(XP,YP)
2080 IF W>1 THEN LINE (XP,YP)-(H(LT),V(LT))
2090 H(LT)=XP:V(LT)=YP
2100 NEXT
2105 VIEW
2110 RETURN
